/*
 * Mike Arduino UNO R4 Wifi
 * RC Car - PERIPHERAL A (Front Wheels)

  HARDWARE: 

  GROUP NUMBER: 50

  ORIGINALITY: 
  Original Chassis Construct 
  Original Motor Algorithms
  Original Circuit Topology
  Original Project Design
  ETC

  Ahmad Awaidah (aawai) (aawai@uic.edu) Arduino UNO R4 Wi-Fi
  2 DEVICES: L298N Driver, 2 DC Motors
  COMMUNICATION: BLE, Serial

  Michael Nguyen (mnguy37) (mnguy37@uic.edu) ELGOO UNO R3
  2 DEVICES: 2 LCD Displays
  COMMUNICATION: Serial

  Mike Vinanzaca (mvina2) (mvina2@uic.edu) Arduino UNO R4 Wi-Fi
  2 DEVICES: L298N Driver, 2 DC Motors
  COMMUNICATION: BLE

  Patrick Thomas (pthom29) (pthom29@uic.edu) Arduino UNO R4 Wi-Fi
  2 DEVICES: LED Light, Joystick
  COMMUNICATION: BLE



 */

// REQUIRED LIBRARIES //
#include <ArduinoBLE.h>

// L298N MOTOR DRIVER PIN IDS //
#define ENA 5
#define IN1 2
#define IN2 3
#define IN3 4
#define IN4 6
#define ENB 9

// FRONT ARDUINO UUIDs //
BLEService carService("19B10000-E8F2-537E-4F6C-D104768A1214"); 
// Characteristic to hold 2 integers (X and Y), so we need 4 bytes
BLECharacteristic commandChar("19B10001-E8F2-537E-4F6C-D104768A1214", BLERead | BLEWrite, 4);


//////////////////////////
// JOYSTICK DATA STRUCT //
//////////////////////////
struct JoystickData {
  int16_t x;
  int16_t y;
};

// STRUCT OBJECT //
JoystickData receivedData;


////////////
// SET UP //
////////////
void setup() {
  Serial.begin(9600);
  
  pinMode(ENA, OUTPUT); pinMode(ENB, OUTPUT);
  pinMode(IN1, OUTPUT); pinMode(IN2, OUTPUT);
  pinMode(IN3, OUTPUT); pinMode(IN4, OUTPUT);
  stopMotors();

  if (!BLE.begin()) {
    Serial.println("starting Bluetooth Low Energy module failed");
    while (1);
  }

  BLE.setLocalName("R4_RC_Car");
  BLE.setAdvertisedService(carService);
  carService.addCharacteristic(commandChar);
  BLE.addService(carService);
  
  // Start advertising so the Remote can find us
  BLE.advertise();
  Serial.println("Bluetooth Car Ready. Waiting for Remote");
}


//////////
// LOOP //
//////////
void loop() {
  BLEDevice central = BLE.central();

  if (central) {
    Serial.print("Connected to Remote: ");
    Serial.println(central.address());

    while (central.connected()) {
      if (commandChar.written()) {
        // Read the raw bytes into our struct
        commandChar.readValue(&receivedData, sizeof(receivedData));
        
        Serial.print("X: "); Serial.print(receivedData.x);
        Serial.print(" Y: "); Serial.println(receivedData.y);
        
        controlFrontMotors(receivedData.x, receivedData.y);
      }
    }
    
    Serial.println("Remote Disconnected");
    stopMotors();
    // When disconnected, it automatically starts advertising again
  }
}

//////////////////////////
// CONTROL FRONT MOTORS //
//////////////////////////
void controlFrontMotors(int x, int y) {
  // Deadzone
  if (x > 450 && x < 570 && y > 450 && y < 570) {
    stopMotors();
    return;
  }

  if (y > 600) move(HIGH, LOW, HIGH, LOW, 150);       // Forward
  else if (y < 400) move(LOW, HIGH, LOW, HIGH, 150);  // Backward
  else if (x < 400) move(LOW, HIGH, HIGH, LOW, 150);  // Left
  else if (x > 600) move(HIGH, LOW, LOW, HIGH, 150);  // Right
}


//////////
// MOVE //
//////////
void move(int n1, int n2, int n3, int n4, int speed) {
  digitalWrite(IN1, n1); digitalWrite(IN2, n2);
  digitalWrite(IN3, n3); digitalWrite(IN4, n4);
  analogWrite(ENA, speed); analogWrite(ENB, speed);
}


/////////////////
// STOP MOTORS //
/////////////////
void stopMotors() {
  digitalWrite(IN1, LOW); digitalWrite(IN2, LOW);
  digitalWrite(IN3, LOW); digitalWrite(IN4, LOW);
  analogWrite(ENA, 0); analogWrite(ENB, 0);
}

/////////
// EOF //
/////////
